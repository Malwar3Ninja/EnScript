/*
Simple extension count aggregator

Copyright: Geoff Black
           geoff@geoffblack.com
*/

class ExtensionCounterClass: NameListClass {
  ulong ExtCount;

  ExtensionCounterClass(NameListClass parent=null, const String &name="", ulong extCount=0):
    NameListClass(parent, name),
    ExtCount = extCount
  {
  }
}

class MainClass {

  void CheckExt(ExtensionCounterClass list, const String &ext) {
    String x = ext;
    if (x == "") {
      x = "*";
    }
    ExtensionCounterClass ext = list.Find(x);
    if (!ext) {
      new ExtensionCounterClass(list, x, 1);
    }
    else {
      ++ext.ExtCount;
    }
  }

  void Main(CaseClass c) {
    SystemClass::ClearConsole();
    ExtensionCounterClass ExtensionList();
#ifdef VERSION_6
    forall (EntryClass e in c.EntryRoot()) {
      if (e.IsSelected()) {
        CheckExt(ExtensionList, e.Extension());
      }
    }
#endif
#ifdef VERSION_7
    ItemIteratorClass iter();
    String selectedTags;
    bool good = iter.Open(c, ItemIteratorClass::PROMPT | ItemIteratorClass::NOPROXY, ItemIteratorClass::TAGGED, selectedTags);
    if (good) {
      while (EntryClass e = iter.GetNextEntry()) {
        CheckExt(ExtensionList, e.Extension());
      }
    }
#endif
    forall (ExtensionCounterClass ext in ExtensionList) {
      Console.WriteLine(ext.Name() + "\t" + ext.ExtCount);
    }
  }
}
